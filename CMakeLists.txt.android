# this is a special version for Android testing

# TODO: why do we need 3.6.0?
cmake_minimum_required (VERSION 3.6.0 FATAL_ERROR)

# TODO: shouldn't this file be somewhere in the NDK??
set (CMAKE_TOOLCHAIN_FILE "${CMAKE_CURRENT_SOURCE_DIR}/unit_tests/android.toolchain.cmake")

set (ANDROID_ABI "x86" CACHE STRING "")
set (ANDROID_TOOLCHAIN "clang" CACHE STRING "")
set (ANDROID_NATIVE_API_LEVEL "android-16" CACHE STRING "")

# set project and version
# warning: do not set the language
project (PEGTL VERSION 2.0.0)

# set C++ language standard
set (CMAKE_CXX_STANDARD 11)
set (CMAKE_CXX_STANDARD_REQUIRED ON)
set (CMAKE_CXX_EXTENSIONS OFF)

# modify flags for testing
if (MSVC)
  set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W4 /WX /utf-8")
else ()
  set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pedantic -Wall -Wextra -Wshadow -Werror")
endif ()

# define a header-only library
add_library (PEGTL INTERFACE)
target_include_directories (PEGTL INTERFACE
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
  $<INSTALL_INTERFACE:include>
)

# testing
enable_testing ()
file (GLOB testsources unit_tests/*.cc)
foreach (testsourcefile ${testsources})
  get_filename_component (exename ${testsourcefile} NAME_WE)
  add_executable (${exename} ${testsourcefile})
  target_link_libraries (${exename} PEGTL)
  add_test (NAME ${exename} WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/unit_tests/run-on-android.sh ${CMAKE_CURRENT_BINARY_DIR}/${exename})
endforeach (testsourcefile)
